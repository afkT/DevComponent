include ':app'

// ====================
// = core - 核心开发库 =
// ====================

def cores = [
        "core",
        "core_base_lib",
        "core_lib_base",
        "core_lib_bean",
        "core_lib_config",
        "core_lib_engine",
        "core_lib_network",
        'core_lib_ui'
]

cores.forEach {
    include(":$it")
    // 属于 core/libs 模块
    if (it.startsWith("core_lib")) {
        // 例 core/libs/lib_engine
        project(":$it").projectDir = new File(
                rootProject.getProjectDir(),
                "/core/libs/" + it.replace("core_", "")
        )
    } else {
        project(":$it").projectDir = new File(rootProject.getProjectDir(), "/core/$it")
    }
}

// =========================
// = Interesting - 规范检测 =
// =========================

include ':DevStandard'
project(':DevStandard').projectDir = new File('interesting/DevStandard')

// ======================
// = libs - 模块封装复用 =
// ======================

def libs = [
        "lib_circle_igview",
        "lib_commodity",
]

libs.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootProject.getProjectDir(), "/libs/$it")
}

// ========================
// = module - 各个模块 APP =
// ========================

def modules = [
        "module_qrcode",
]

modules.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootProject.getProjectDir(), "/module/$it")
}