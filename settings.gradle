include ':app'

// ===================
// = core - 核心开发库 =
// ===================

def cores = [
        "core",
        "core_base_lib",
        "core_lib_base",
        "core_lib_bean",
        "core_lib_config",
        "core_lib_engine",
        "core_lib_environment",
        "core_lib_network",
        "core_lib_receiver",
        "core_lib_router",
        "core_lib_ui",
        "core_lib_upload",
        "core_lib_utils",
        "core_lib_web",
]

cores.forEach {
    include(":$it")
    // 属于 core/libs 模块
    if (it.startsWith("core_lib")) {
        // 例 core/libs/lib_engine
        project(":$it").projectDir = new File(
                rootDir, "/component/core/libs/" + it.replace("core_", "")
        )
    } else {
        project(":$it").projectDir = new File(rootDir, "/component/core/$it")
    }
}

// ====================
// = libs - 模块封装复用 =
// ====================

def libs = [
        "lib_circle_igview",
        "lib_commodity",
]

libs.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootDir, "/component/libs/$it")
}

// =======================
// = module - 各个模块 APP =
// =======================

def modules = [
        // 功能模板
        "module_template",

        // ==============
        // = 各个功能模块 =
        // ==============

        "module_commodity",
        "module_main",
        "module_splash",
        "module_user",
]

modules.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootDir, "/component/module/$it")
}

// ========================
// = Interesting - 规范检测 =
// ========================

def interestings = [
        "DevReplace",
        "DevStandard",
]

interestings.forEach {
    include(":$it")
    project(":$it").projectDir = new File(rootDir, "interesting/$it")
}